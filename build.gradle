group 'com.github.simonpercic.responseecho'
version VERSION_NAME

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.6.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

mainClassName = "com.github.simonpercic.responseecho.Application"

jar {
    baseName = 'responseecho'
    version = VERSION_NAME
}

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.retrofitVersion = '2.1.0'

dependencies {
    compile 'com.github.simonpercic:oklog-shared:2.2.0'

    // Spring Framework
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'

    compile 'org.webjars:bootstrap:3.3.6'
    compile 'com.google.code.gson:gson:2.7'
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile 'commons-io:commons-io:2.5'

    // Unit test dependencies
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

task stage(type: Copy, dependsOn: [clean, assemble]) {
    from jar.archivePath
    into project.rootDir
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}
